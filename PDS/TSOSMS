         TITLE 'TSOSMS - LIST DATASET SMS STATUS'
*=====================================================================*
* INPUT PARMS :                                                       *
* (R2) 1. DSNAME - TARGET OF THE SMS STATUS                           *
* (R3) 2. DSN L' - DSNAME LENGTH                                      *
* (R4) 3. RCRS   - RETURN CODE/REASON CODE                            *
* (R5) 4. SC     - STORAGE CLASS                                      *
* (R6) 5. MC     - MANAGEMENT CLASS                                   *
* (R7) 6. DC     - DATA CLASS                                         *
*=====================================================================*
         SPACE
TSOSMS   CSECT
         USING *,R15
         B     AROUND
         DC    AL1(23)
         DC    CL9'&SYSDATE'
         DC    CL5'&SYSTIME'
AROUND   DS    0H
         STM   R14,R12,12(R13)
         ST    R13,SAVEAREA+4
         LA    R7,SAVEAREA
         ST    R7,8(R13)
         LA    R13,SAVEAREA
         DROP  R15
         USING SAVEAREA,R13
         B     BEGIN
SAVEAREA DC    18F'0'
BEGIN    DS    0H
         LM    R2,R7,0(R1)             LOAD PARM ADDRS
         MVC   DSN(46),0(R2)           SETUP DSNAME
         CLC   DSN(1),TIC              TICKIE MARKS PRESENT ?
         BNE   NOTICKIE                NO..PROCEED
         MVC   DSN2(46),DSN            Y...PREP IT
         TR    DSN2(46),TABLE2         CONVERT TICS TO BLANKS
         MVC   DSN(44),DSN2+1          DROP 1ST & LAST CHARS
         PACK  DOUB(8),0(2,R3)         ZONED DECIMAL FROM REXX
         SP    DOUB,=P'2'              DEDUCT FOR TICS
         B     COMMON
NOTICKIE EQU   *
         PACK  DOUB(8),0(2,R3)         ZONED DECIMAL FROM REXX
COMMON   EQU   *
         CVB   R15,DOUB                WE NEED
         ST    R15,DSNL                HEXADECIMAL
         LINK  EP=IGWASMS,MF=(E,PARMS) CALL SMS
         ST    R15,SAVE15              NO..ELABORATE
* RETURN CODE
         L     R15,RC
         CVD   R15,DOUB
         UNPK  WORK3(3),DOUB+6(2)
         MVZ   WORK3+2(1),=X'00'
         TR    WORK3+2(1),TABLE
         MVC   0(2,R4),WORK3+1         RC TO CALLER
* REASON CODE
         L     R15,RS
         CVD   R15,DOUB
         UNPK  WORK3(3),DOUB+6(2)
         MVZ   WORK3+2(1),=X'00'
         TR    WORK3+2(1),TABLE
         MVC   2(2,R4),WORK3+1         RS TO CALLER
* ===== END NON-ZERO CODE ===========
*
ITSMS    EQU   *
         MVC   2(30,R5),SC             STORAGE CLASS
         MVC   2(30,R6),MC             MANAGEMENT CLASS
         MVC   2(30,R7),DC             DATA CLASS
*
EXIT     EQU   *
         L     R13,SAVEAREA+4
         RETURN (14,12),RC=(15)
         SPACE 3
         LTORG
         EJECT
*=====================================================================*
*                         D A T A   A R E A S                         *
*=====================================================================*
         SPACE
DOUB     DS   D
SAVE15   DS   F
WORK3    DS   CL3
TABLE    DC   C'0123456789ABCDEF'
*
PARMS    DS   0F
ARC      DC   A(RC)        A(RETURN CODE - R15)
ARS      DC   A(RS)        A(REASON CODE - R0)
APROBDET DC   A(PROBDET)   A(RC,RS)
ADSNL    DC   A(DSNL)      DATASET NAME L'
ADSN     DC   A(DSN)       A(DATASET NAME)
ASMSDATA DC   A(SMSDATA)   A(SMS DATA)
ADSTYPE  DC   A(DSTYPE)    A(DATASET TYPE)
         SPACE 3
RC       DC   F'0'
RS       DC   F'0'
PROBDET  DC   2F'0'
DSNL     DC   F'0'
DSN      DC   CL46' '
SMSDATA  DS   0CL90
SC       DC   CL30' '
MC       DC   CL30' '
DC       DC   CL30' '
DSTYPE   DC   F'0'
*
TIC      DC   C''''
DSN2     DC   CL46' '
TABLE2   DS   0CL256
         DC   X'000102030405060708090A0B0C0D0E0F'
         DC   X'101112131415161718191A1B1C1D1E1F'
         DC   X'202122232425262728292A2B2C2D2E2F'
         DC   X'303132333435363738393A3B3C3D3E3F'
         DC   X'404142434445464748494A4B4C4D4E4F'
         DC   X'505152535455565758595A5B5C5D5E5F'
         DC   X'606162636465666768696A6B6C6D6E6F'
         DC   X'707172737475767778797A7B7C',X'40',X'7E7F'
         DC   X'808182838485868788898A8B8C8D8E8F'
         DC   X'909192939495969798999A9B9C9D9E9F'
         DC   X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'
         DC   X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC   X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC   X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC   X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC   X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
         SPACE 3
R0       EQU  0
R1       EQU  1
R2       EQU  2
R3       EQU  3
R4       EQU  4
R5       EQU  5
R6       EQU  6
R7       EQU  7
R8       EQU  8
R9       EQU  9
R10      EQU  10
R11      EQU  11
R12      EQU  12
R13      EQU  13
R14      EQU  14
R15      EQU  15
         END
